plugins {
	id "com.jfrog.bintray" version "1.7.3"
	id "maven-publish"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "thombergs"
			name "Tom Hombergs"
			email "tom.hombergs@gmail.com"
		}
	}

	scm {
		url "https://github.com/adessoag/wicked-forms-wicket6"
	}
}

publishing {
	publications {
		BintrayPublication(MavenPublication) {
			from components.java
			artifact sourcesJar {
				classifier "sources"
			}
			artifact javadocJar {
				classifier "javadoc"
			}
			groupId 'de.adesso'
			artifactId 'wicked-forms-wicket-6'
			version bintrayVersion
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'API to create a dynamic web form from an object representation with Apache Wicket 6.')
				root.appendNode('name', 'Wicked Forms')
				root.appendNode('url', 'https://github.com/adessoag/wicked-forms')
				root.children().last() + pomConfig
			}
		}
	}
}

bintray {
	user = bintrayUser
	key = bintrayKey
	publications = ['BintrayPublication']

	pkg {
		repo = 'wicked-forms'
		name = 'wicked-forms-wicket6'
		userOrg = 'adesso'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/adessoAG/wicked-forms.git'
		version {
			name = bintrayVersion
			desc = bintrayVersion
			released  = new Date()
		}
	}

}

dependencies {
	compile "org.apache.wicket:wicket-core:7.9.0"
	compile project(":wicked-forms:wicked-forms-model")
	testCompile 'junit:junit:4.12'
}

jar {
	from('src/main/java') {
		include '**/*.html'
		include '**/*.properties'
	}
}

